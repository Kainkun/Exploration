title: TrashJob
position: 501,-339
---
<<declare $trashJobStarted to false>>
<<declare $trashJobCompleted to false>>
<<declare $trashCount to 0>>
<<declare $depositedTrashCount to 0>>

<<if $trashJobStarted is false>>
    <<if $trashCount > 10>>
        <<jump TrashJobCompletedEarly>>
    <<else>>
        <<jump TrashJobStart>>
    <<endif>>
<<endif>>

<<if $trashJobCompleted is false>>
    <<jump TrashJobDuring>>
<<endif>>

<<jump TrashJobAfter>>
===

title: TrashJobCompletedEarly
position: 773,-496
---
BruhBot: Hey can you pick up 10 pieces of trash?
BruhBot: Oh wtf you already did that?
BruhBot: Well put the trash in the bin. It will give you a jobToken for every 10 trash you turn in.
<<set $trashJobStarted to true>>
<<set $trashJobCompleted to true>>
===

title: TrashJobStart
position: 773,-396
---
BruhBot: Bruh its dirty here, go pick up 10 pieces of trash. I'll give you a jobToken for it.
BruhBot: Heres a trash collector module for your multi tool.
<<SetActiveName BruhBot trashCollector>>
<<if $hasMultiTool is false>>
    BruhBot: Bruh you dont have a MultiTool? WelcomeBot should have one for you. Once you get that, youll be able to use the trash collector
<<endif>>
<<set $trashJobStarted to true>>
===

title: TrashJobDuring
position: 773,-311
---
<<if ($trashCount + $depositedTrashCount) >= 10>>
    BruhBot: Good job bruh. Put the trash in the bin. It will give you a jobToken for every 10 trash you turn in.
    <<set $trashJobCompleted to true>>
<<else>>
    BruhBot: Bruh, you only collected {($trashCount + $depositedTrashCount)} trash, go get at least 10 then come back here.
<<endif>>
===

title: TrashJobAfter
position: 773,-232
---
BruhBot: Sup Bruh
===


title: LibraryJobs
position: 501,410
---

<<if $libraryFixMachineJobStarted is false>>
    RepairBot: I got a couple of jobs for you.
<<endif>>

<<if $libraryFixMachineJobCompleted is false>>
    <<jump LibraryFixMachineJob>>
<<endif>>

<<if $LibraryFixVulcanJobCompleted is false>>
    <<jump LibraryFixVulcanJob>>
<<endif>>

RepairBot: Thanks for doing both of these Jobs
===



title: LibraryFixMachineJob
tags: 
colorID: 8
position: 501,676
---
<<declare $libraryFixMachineJobStarted to false>>
<<declare $libraryFixMachineJobCompleted to false>>
<<declare $libraryMachineFixed to false>>

<<if $libraryFixMachineJobStarted is false>>
    <<if $libraryMachineFixed is true>>
        <<jump LibraryFixMachineJobCompletedEarly>>
    <<else>>
        <<jump LibraryFixMachineJobStart>>
    <<endif>>
<<endif>>

<<if $libraryFixMachineJobCompleted is false>>
    <<jump LibraryFixMachineJobDuring>>
<<endif>>

//<<jump LibraryFixMachineJobAfter>>
===

title: LibraryFixMachineJobCompletedEarly
tags: 
colorID: 8
position: 773,517
---
RepairBot: Your first job is to fix the library machine.
RepairBot: Oh. you already did? Well you deserve a jobToken for that. Here.
<<set $libraryFixMachineJobStarted to true>>
<<set $libraryFixMachineJobCompleted to true>>
<<set $jobTokenCount += 1>>
<<jump LibraryFixVulcanJob>>
===

title: LibraryFixMachineJobStart
tags: 
colorID: 8
position: 773,620
---
RepairBot: Your first job is to fix the library machine. Come back here after and ill give you a jobToken.
<<set $libraryFixMachineJobStarted to true>>
===

title: LibraryFixMachineJobDuring
tags: 
colorID: 8
position: 773,724
---
<<if $libraryMachineFixed is true>>
    RepairBot: Thanks for fixing the library machine. Heres the jobToken.
    <<set $jobTokenCount += 1>>
    <<set $libraryFixMachineJobCompleted to true>>
    <<jump LibraryFixVulcanJob>>
<<else>>
    RepairBot: Hey, did you fix the library machine yet?
    -> I'm still working on it.
        RepairBot: Alright, come back when you're done.
    -> Can you help me out?
        RepairBot: The library machine's main control panel is on the second floor.
<<endif>>
===



title: LibraryFixVulcanJob
position: 501,139
---
<<declare $LibraryFixVulcanJobStarted to false>>
<<declare $LibraryFixVulcanJobCompleted to false>>
<<declare $libraryVulcanFixed to false>>

<<if $LibraryFixVulcanJobStarted is false>>
    <<if $libraryVulcanFixed is true>>
        <<jump LibraryFixVulcanJobCompletedEarly>>
    <<else>>
        <<jump LibraryFixVulcanJobStart>>
    <<endif>>
<<endif>>

<<if $LibraryFixVulcanJobCompleted is false>>
    <<jump LibraryFixVulcanJobDuring>>
<<endif>>

<<jump LibraryFixVulcanJobAfter>>
===

title: LibraryFixVulcanJobCompletedEarly
position: 773,-15
---
RepairBot: And for your next job, fix the vulcan thingy.
RepairBot: Oh, you already did? We'll heres your jobToken for that.
<<set $LibraryFixVulcanJobStarted to true>>
<<set $LibraryFixVulcanJobCompleted to true>>
<<set $jobTokenCount += 1>>
===

title: LibraryFixVulcanJobStart
position: 773,93
---
RepairBot: For your next job, can you go fix the Vulcan thingy? It's in the presentation room.
<<set $LibraryFixVulcanJobStarted to true>>
===

title: LibraryFixVulcanJobDuring
position: 773,194
---
<<if $libraryVulcanFixed is true>>
    RepairBot: Thanks! Here's a jobToken
    <<set $jobTokenCount += 1>>
    <<set $LibraryFixVulcanJobCompleted to true>>
<<else>>
    RepairBot: Hey, did you fix the vulcan thingy yet?
    -> I'm still working on it.
        RepairBot: Alright, come back when you're done.
    -> Can you help me out?
        RepairBot: The vulcan thingy is in the presentation room. If its currently being used by someone the door will be locked. Of course then the only way in is from the stairs on the roof but there is absolutely clearly no way to the roof of the presentation room I positively guarantee it...
<<endif>>
===

title: LibraryFixVulcanJobAfter
position: 773,302
---
If you're reading this, something went wrong.
===